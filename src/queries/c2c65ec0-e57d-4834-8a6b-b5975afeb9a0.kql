// Query: Pivoted effective cost by month, sub-account, and resource group, including totals. Useful for cost allocation and business unit reporting.
// Query ID: c2c65ec0-e57d-4834-8a6b-b5975afeb9a0
// Description: Pivoted effective cost by month, sub-account, and resource group, including totals. Useful for cost allocation and business unit reporting.
// Used Variables: `CostsByMonth`

let monthname = dynamic(['', 'January', 'February', 'March', 'April', 'May', 'June', 'July', 'August', 'September', 'October', 'November', 'December']);
let data = (
    CostsByMonth
    | extend x_ResourceGroupId = strcat(SubAccountId, '/resourcegroups/', x_ResourceGroupName)
    | summarize 
        EffectiveCost  = sum(EffectiveCost),
        SubAccountName = take_any(SubAccountName),
        x_ResourceGroupName = take_any(x_ResourceGroupName)
        by
        ChargePeriodStart,
        x_ResourceGroupId
    | as per
    | union (
        per
        | summarize 
            EffectiveCost  = sum(EffectiveCost),
            SubAccountName = take_any(SubAccountName),
            x_ResourceGroupName = take_any(x_ResourceGroupName)
            by
            x_ResourceGroupId
    )
    | order by ChargePeriodStart asc
    | extend EffectiveCost = todouble(round(EffectiveCost, 2))
    | extend ChargePeriod = iff(isempty(ChargePeriodStart), strcat('Total'), strcat(format_datetime(ChargePeriodStart, 'yyyy-MM - '), monthname[monthofyear(ChargePeriodStart)]))
);
data | evaluate pivot(ChargePeriod, sum(EffectiveCost), x_ResourceGroupName, SubAccountName)
| order by Total desc

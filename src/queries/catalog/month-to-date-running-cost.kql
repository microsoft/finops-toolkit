// ============================================================================
// Query: Running Cost This Month
// Description:
//   Returns the running daily effective cost for the current month.
//   Useful for tracking cumulative spend and daily cost trends as the month progresses.
// Author: FinOps Toolkit
// Parameters:
//   (none)
// Output:
//   Each row represents a day in the current month with the running total of effective cost.
// Usage:
//   Use this query to monitor and visualize cost accumulation throughout the current month.
// Last Tested: 2025-05-17
// =========================================================================

let monthname = dynamic(['', 'January', 'February', 'March', 'April', 'May', 'June', 'July', 'August', 'September', 'October', 'November', 'December']);
Costs()
| where ChargePeriodStart >= startofmonth(now())
| summarize EffectiveCost = sum(EffectiveCost) by ChargePeriodStart, Month = strcat(format_datetime(ChargePeriodStart, 'MM '), monthname[monthofyear(ChargePeriodStart)])
| order by ChargePeriodStart asc
| extend EffectiveCostRunningTotal = row_cumsum(EffectiveCost)
| project ChargePeriodStart, EffectiveCostRunningTotal, Month
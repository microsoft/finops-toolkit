advisorresources
| where type == 'microsoft.advisor/recommendations'
| where properties.category == 'Cost'
| extend 
    solution = tostring(properties.shortDescription.solution)
| where not(solution has_cs "reserved instance")
| where not(solution has_cs "savings plan")
| where solution != "Right-size or shutdown underutilized virtual machines"
| extend 
    savings = todouble(properties.extendedProperties.savingsAmount),
    resourceId = tostring(properties.resourceMetadata.resourceId),
    currency = tostring(properties.extendedProperties.savingsCurrency)
| summarize 
    sum_savings = sum(savings), 
    dcount_resources = dcount(resourceId), 
    currency = any(currency)
    by solution
| join kind=leftouter (
    advisorresources
    | where type == 'microsoft.advisor/recommendations'
    | where properties.category == 'Cost'
    | extend 
        x_RecommendationSolution = tostring(properties.shortDescription.solution)
    | where not(x_RecommendationSolution has_cs "reserved instance")
    | where not(x_RecommendationSolution has_cs "savings plan")
    | where x_RecommendationSolution != "Right-size or shutdown underutilized virtual machines"
    | extend 
        x_RecommendationId = id,
        x_ResourceGroupName = tolower(resourceGroup),
        SubAccountId = subscriptionId,
        x_RecommendationCategory = tostring(properties.category),
        x_RecommendationImpact = tostring(properties.impact),
        x_RecommendationProvider = 'Azure.Advisor',
        x_RecommendationTypeId = tostring(properties.recommendationTypeId),
        x_RecommendationControl = 'Check Advisor',
        rawMaturity = tostring(properties.extendedProperties.maturityLevel)
    | extend
        x_RecommendationMaturityLevel = iif(isnotempty(rawMaturity), rawMaturity, "GA")
    | extend
        x_RecommendationDescription = tostring(properties.shortDescription.problem),
        ResourceId = tolower(properties.resourceMetadata.resourceId),
        x_ResourceType = tolower(properties.impactedField),
        ResourceName = tolower(properties.impactedValue),
        x_RecommendationDate = tostring(properties.lastUpdated)
) on $left.solution == $right.x_RecommendationSolution
| extend 
    savingsText = iif(sum_savings == 0, "no data", strcat(tostring(sum_savings), " ", currency))
| extend 
    x_RecommendationDetails = strcat("Resources: ", tostring(dcount_resources), "; Savings: ", savingsText)
| project 
    x_RecommendationId,
    x_ResourceGroupName,
    SubAccountId,
    x_RecommendationCategory,
    x_RecommendationImpact,
    x_RecommendationProvider,
    x_RecommendationTypeId,
    x_RecommendationControl,
    x_RecommendationMaturityLevel,
    x_RecommendationDescription,
    x_RecommendationSolution,
    ResourceId,
    x_ResourceType,
    ResourceName,
    x_RecommendationDetails,
    x_RecommendationDate

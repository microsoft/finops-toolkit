advisorresources
| where type == 'microsoft.advisor/recommendations'
| where properties.category == 'Cost'
| project 
    x_RecommendationId = id,
    x_ResourceGroupName = tolower(resourceGroup),
    SubAccountId = subscriptionId,
    x_RecommendationCategory = tostring(properties.category),
    x_RecommendationImpact = tostring(properties.impact),
    x_RecommendationProvider = 'Azure.Advisor',
    x_RecommendationTypeId = tostring(properties.recommendationTypeId),
    x_RecommendationControl = tostring(properties.extendedProperties.recommendationControl),
    x_RecommendationMaturityLevel = tostring(properties.extendedProperties.maturityLevel),
    x_RecommendationDescription = tostring(properties.shortDescription.problem),
    x_RecommendationSolution = tostring(properties.shortDescription.solution),
    ResourceId = tolower(properties.resourceMetadata.resourceId),
    x_ResourceType = tolower(properties.impactedField),
    ResourceName = tolower(properties.impactedValue),
    x_RecommendationDetails = tostring(properties.extendedProperties),
    x_RecommendationDate = tostring(properties.lastUpdated)
| join kind=leftouter (
    resourcecontainers
    | where type == 'microsoft.resources/subscriptions'
    | project 
        SubAccountName = name, 
        SubAccountId = subscriptionId
) on SubAccountId
| project-away SubAccountId1
